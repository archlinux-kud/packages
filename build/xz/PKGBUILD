# Maintainer: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Fran√ßois Charette <firmicus@gmx.net>

pkgname=xz
pkgver=5.5.2beta
pkgrel=2
epoch=1
pkgdesc='Library and command line tools for XZ and LZMA compressed files'
arch=('x86_64')
url='https://xz.tukaani.org/xz-utils/'
license=(
  '0BSD'
  'CC-BY-SA-4.0'
  'GPL-2.0-only'
  'GPL-2.0-or-later'
  'LGPL-2.1-or-later'
  'LicenseRef-scancode-public-domain'
)
depends=('glibc' 'sh')
makedepends=('git' 'po4a' 'doxygen')
provides=('liblzma.so')
validpgpkeys=('3690C240CE51B4670D30AD1C38EE757D69184620') # Lasse Collin <lasse.collin@tukaani.org>
_commit=6f1790254a03c5edf0f2976f773220f070450acd  # refs/tags/v5.5.2beta
source=("git+https://git.tukaani.org/xz.git#commit=${_commit}")
sha256sums=('8d8852c6dc955e1ad4305fe660318662f4b440d01da19fd6b647dce91b845b3f')
sha512sums=('3d762ce4907e6023a6448d6683f1ff45e4b99e97c0a87f9e40fe1fdc336e26afd7471af90b3e50821e1dcc2e446787815787c8322a8988e064ae35aac6a77727')

prepare() {
  cd ${pkgname}

  ./autogen.sh
}

build() {
  cd ${pkgname}

  ./configure \
    --prefix=/usr \
    --disable-ifunc \
    --disable-rpath \
    --enable-werror
  make
}

check() {
  cd ${pkgname}

  make check
}

package() {
  cd ${pkgname}

  make DESTDIR="${pkgdir}" install

  install -d -m0755 "${pkgdir}/usr/share/licenses/xz/"
  ln -sf /usr/share/doc/xz/COPYING "${pkgdir}/usr/share/licenses/xz/"
  ln -sf /usr/share/doc/xz/COPYING.0BSD "${pkgdir}/usr/share/licenses/xz/"
}
