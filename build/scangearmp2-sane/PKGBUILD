# Maintainer: Albert I <kras@raphielgang.org>
# Contributor: Paul van der Linden <mail@paultjuh.org>

pkgname=scangearmp2-sane
pkgver=4.60.0
pkgrel=1
pkgdesc='Canon ScanGear MP v2 scanner utility and sane backend'
arch=('x86_64')
url='https://github.com/ThierryHFR/scangearmp2'
license=('GPL2' 'custom:canon')
depends=('glib2' 'gtk3' 'libusb')
makedepends=('cmake' 'libjpeg-turbo' 'sane' 'intltool')
optdepends=('libjpeg-turbo: sane backend plug-in'
            'sane: sane backend plug-in')
provides=('scangearmp2')
conflicts=('scangearmp2')
_srcname="${pkgname%-sane}"
source=("${_srcname}-${pkgver}.tar.gz::${url}/releases/download/${pkgver}/${_srcname}-${pkgver}.tar.gz")
noextract=("${_srcname}-${pkgver}.tar.gz") # root folder might be different than what we expect
sha384sums=('6bb62ba0286c00179da890afddd5d893f7524f68eb0bea438bd53632375f39be28338418e971c29edcfec16a36510603')
b2sums=('652d5fabea1eb3063904f27d4c48424fe1a680dadef4f710d1f0af1f41fbd86477a2f0d171a645bfa8913e6e7a8556ad6d10a5c0d808c0b3d480b361401b494c')

prepare() {
    mkdir "${_srcname}-${pkgver}"
    tar xf "${_srcname}-${pkgver}.tar.gz" -C "${_srcname}-${pkgver}" --strip-components 1
}

build() {
    cmake -B build -S "${_srcname}-${pkgver}" -DCMAKE_INSTALL_PREFIX="/"
    make -C build
}

package() {
    DESTDIR="${pkgdir}" make -C build install
    install -d "${pkgdir}/usr/share/licenses"
    mv "${pkgdir}/usr/share/scangearmp2/doc" "${pkgdir}/usr/share/licenses/${pkgname}"
}
