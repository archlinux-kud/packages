# Maintainer: Albert I <kras@raphielgang.org>
# Contributor: Paul van der Linden <mail@paultjuh.org>

pkgname=scangearmp2-sane
pkgver=4.70a
pkgrel=2
pkgdesc='Canon ScanGear MP v2 scanner utility and sane backend'
arch=('x86_64')
url='https://github.com/ThierryHFR/scangearmp2'
license=(
    'GPL-2.0-only'
    'LicenseRef-Canon'
)
depends=(
    'gcc-libs'
    'glib2' 'libglib-2.0.so' 'libgobject-2.0.so'
    'glibc'
    'gtk3' 'libgdk-3.so' 'libgtk-3.so'
    'libusb' 'libusb-1.0.so'
)
makedepends=(
    'cmake'
    'intltool'
    'libjpeg'
    'sane'
)
optdepends=(
    'libjpeg: sane backend plug-in'
    'sane: sane backend plug-in'
)
provides=('scangearmp2')
conflicts=('scangearmp2')
_srcname="${pkgname%-sane}"
source=("${_srcname}-${pkgver}.tar.gz::${url}/releases/download/${pkgver}/${_srcname}-${pkgver}.tar.gz")
noextract=("${_srcname}-${pkgver}.tar.gz") # root folder might be different than what we expect
sha384sums=('4864e7b65a6093dfc1b9fee7ee052da62172d0e7ef439d24abd866e479e272337634077d811306b603ab529e86612e45')
b2sums=('9004de78c99ec98e2ae5f7869bc792b75fdf9324527aa54243bde1220944aff647fa81d9429d2d4d2b3402c39e2f06e6e8e150dad589fe9e72498b1ea1b0b430')

prepare() {
    mkdir "${_srcname}-${pkgver}"
    tar xf "${_srcname}-${pkgver}.tar.gz" -C "${_srcname}-${pkgver}" --strip-components 1
}

build() {
    cmake -B build -S "${_srcname}-${pkgver}" -DCMAKE_INSTALL_PREFIX="/"
    make -C build
}

package() {
    DESTDIR="${pkgdir}" make -C build install
    install -d "${pkgdir}/usr/share/licenses"
    mv "${pkgdir}/usr/share/scangearmp2/doc" "${pkgdir}/usr/share/licenses/${pkgname}"
}
