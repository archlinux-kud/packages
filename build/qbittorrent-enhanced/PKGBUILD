# Maintainer: Albert I <kras@raphielgang.org>
# Contributor: Leo <i@setuid0.dev>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG
# Contributor: Jkkyll Wu <adaptee at gmail [dot] com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgbase=qbittorrent-enhanced
pkgname=("${pkgbase}"{,-nox,-qt5})
pkgver=4.6.0.10
pkgrel=1
pkgdesc='An advanced BitTorrent client programmed in C++, based on Qt toolkit and libtorrent-rasterbar (Enhanced Edition)'
arch=('x86_64')
url='https://github.com/c0re100/qBittorrent-Enhanced-Edition'
license=('GPL3' 'custom')
makedepends=('boost' 'cmake' 'git' 'libtorrent-rasterbar' qt{5,6}-{base,tools,svg})
optdepends=('python: needed for torrent search tab')
_commit=22445bd2264dfaac0713d0f308dc16062b0a4000 # refs/tags/release-4.6.0.10
source=("${pkgbase}::git+${url}.git#commit=${_commit}?signed")
sha256sums=('SKIP')
# gpg --import <(curl -s https://github.com/c0re100.gpg)
validpgpkeys=('1BF487001BD0390EE94458427C3B3004FE745AAF') # c0re100 <corehusky@gmail.com>

build() {
  msg2 "Building for Qt6..."
  cmake -B build -S "${pkgbase}" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT6=ON
  cmake --build build

  msg2 "Building for nox..."
  cmake -B build-nox -S "${pkgbase}" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT6=ON \
    -DGUI=OFF \
    -DSYSTEMD=ON
  cmake --build build-nox

  msg2 "Building for Qt5..."
  cmake -B build-qt5 -S "${pkgbase}" \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build-qt5
}

package_qbittorrent-enhanced() {
  depends=('libtorrent-rasterbar' 'qt6-base' 'qt6-svg' 'hicolor-icon-theme')
  provides=('qbittorrent')
  conflicts=('qbittorrent')

  DESTDIR="${pkgdir}" cmake --install build
  install -Dm644 "${pkgbase}/COPYING" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_qbittorrent-enhanced-nox() {
  pkgdesc+=', w/o gui'
  depends=('libtorrent-rasterbar' 'qt6-base')
  provides=('qbittorrent-nox')
  conflicts=('qbittorrent-nox')

  DESTDIR="${pkgdir}" cmake --install build-nox
  install -Dm644 "${pkgbase}/COPYING" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_qbittorrent-enhanced-qt5() {
  pkgdesc+=', built against Qt5'
  depends=('libtorrent-rasterbar' 'qt5-base' 'qt5-svg' 'hicolor-icon-theme')
  provides=('qbittorrent')
  conflicts=('qbittorrent')

  DESTDIR="${pkgdir}" cmake --install build-qt5
  install -Dm644 "${pkgbase}/COPYING" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
