# Maintainer: Albert I <kras@raphielgang.org>
# Contributor: Leo <i@setuid0.dev>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG
# Contributor: Jkkyll Wu <adaptee at gmail [dot] com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgbase=qbittorrent-enhanced
pkgname=(
  'qbittorrent-enhanced'
  'qbittorrent-enhanced-nox'
)
pkgver=4.6.4.10
pkgrel=1
pkgdesc='An advanced BitTorrent client programmed in C++, based on Qt toolkit and libtorrent-rasterbar (Enhanced Edition)'
arch=('x86_64')
url='https://github.com/c0re100/qBittorrent-Enhanced-Edition'
license=('GPL-3.0-or-later WITH cryptsetup-OpenSSL-exception')
makedepends=(
  'boost'
  'cmake'
  'git'
  'libtorrent-rasterbar'
  'qt6-base'
  'qt6-tools'
  'qt6-svg'
)
optdepends=(
  'python: needed for torrent search tab'
)
_commit=535a622c53cb20f0bd9bb7737e25c07f093ad4f9  # refs/tags/release-4.6.4.10
source=("${pkgbase}::git+${url}.git#commit=${_commit}?signed")
sha384sums=('32ff2aca24f1fe75bd895c2f9062bce49a6eb96041dc4da0abc53d6aff46a3a1784ac28586adcc1812e6a6126b720d50')
b2sums=('88ec7aa29ba1db72f8ff63761c1edf3c84e784d55cad267a14712d673487ae76e9299c87d7307e5eaa745daaeb0e43cc5d433be680bce64c93a7e5bf3f2d0539')
validpgpkeys=('1BF487001BD0390EE94458427C3B3004FE745AAF') # c0re100 <corehusky@gmail.com>

build() {
  echo "Building for Qt6..."
  cmake -B build -S "${pkgbase}" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT6=ON
  cmake --build build

  echo "Building for nox..."
  cmake -B build-nox -S "${pkgbase}" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT6=ON \
    -DGUI=OFF \
    -DSYSTEMD=ON
  cmake --build build-nox
}

package_qbittorrent-enhanced() {
  depends=(
    'gcc-libs'
    'glibc'
    'hicolor-icon-theme'
    'libtorrent-rasterbar'
    'openssl' 'libcrypto.so'
    'qt6-base'
    'qt6-svg'
    'zlib' 'libz.so'
  )
  provides=('qbittorrent')
  conflicts=('qbittorrent')

  DESTDIR="${pkgdir}" cmake --install build
  install -Dm644 "${pkgbase}/COPYING" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_qbittorrent-enhanced-nox() {
  pkgdesc+=', w/o gui'
  depends=(
    'gcc-libs'
    'glibc'
    'libtorrent-rasterbar'
    'openssl' 'libcrypto.so'
    'qt6-base'
    'zlib' 'libz.so'
  )
  provides=('qbittorrent-nox')
  conflicts=('qbittorrent-nox')

  DESTDIR="${pkgdir}" cmake --install build-nox
  install -Dm644 "${pkgbase}/COPYING" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
