# Maintainer: Albert I <kras@raphielgang.org>
# Contributor: Ali Molaei <ali dot molaei at protonmail dot com>

pkgname=python-proton-vpn-session
pkgver=0.6.5
pkgrel=1
pkgdesc='Proton VPN session wrapper'
arch=('any')
url='https://github.com/ProtonVPN/python-proton-vpn-session'
license=('GPL-3.0-or-later')
groups=('ProtonVPN')
depends=(
    'python'
    'python-cryptography'
    'python-distro'
    'python-proton-core'
    'python-proton-vpn-logger'
    'python-pynacl'
)
makedepends=(
    'git'
    'python-build'
    'python-installer'
    'python-setuptools'
    'python-wheel'
)
_commit=5864beddfc638d802d388dfefafb414afbdedd61  # refs/tags/v0.6.5
source=("git+https://github.com/ProtonVPN/python-proton-vpn-session.git#commit=${_commit}?signed")
sha256sums=('SKIP')
validpgpkeys=('3DEF8AAF4E4379A665EB31F355F2D687A9C0C999')  # Alexandru Cheltuitor (Gitlab Protontech) <alexandru.cheltuitor@proton.ch>

build() {
    cd "${pkgname}"
    python -m build -w -n
}

package() {
    cd "${pkgname}"
    python -m installer -d "${pkgdir}" dist/*.whl
}
