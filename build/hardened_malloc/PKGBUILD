# Maintainer: Albert I <kras@raphielgang.org>
# Contributor: Thibaut Sautereau (thithib) <thibaut at sautereau dot fr>

pkgname=hardened_malloc
pkgver=12.r1.g8d5c631
pkgrel=2
epoch=2
pkgdesc='Hardened allocator designed for modern systems'
arch=('aarch64' 'x86_64')
url='https://github.com/GrapheneOS/hardened_malloc'
license=('MIT')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'git'
)
optdepends=(
  # https://github.com/GrapheneOS/hardened_malloc#compatibility; not really required
  'openssh: mprotect PROT_READ|PROT_WRITE syscalls in seccomp-bpf filter'
)
checkdepends=('python')
provides=(libhardened_malloc{,-light}="${pkgver%%.*}")
# use latest commit that passes CI tests: https://github.com/GrapheneOS/hardened_malloc/actions
source=("git+${url}.git#commit=8d5c63122499169df5fd9ed6e91fb116512a4745")
sha256sums=('SKIP')
validpgpkeys=('65EEFE022108E2B708CBFCF7F9E712E59AF5F22A') # Daniel Micay <danielmicay@gmail.com>

pkgver() {
  # Avoid matching GrapheneOS Android release tags
  git -C "${pkgname}" describe --long --match '[0-9][! -]' HEAD | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  local _common_flags=(
    'CONFIG_EXAMPLE=false'
    'CONFIG_NATIVE=false'
    'CONFIG_WERROR=false'
  )

  make -C "${pkgname}" "${_common_flags[@]}" VARIANT=default
  make -C "${pkgname}" "${_common_flags[@]}" VARIANT=light
}

check() {
  # Only tests the default variant
  make -C "${pkgname}" test
}

package() {
  install -Dm755 -t "${pkgdir}/usr/lib" "${pkgname}/out/libhardened_malloc.so"
  install -Dm755 -t "${pkgdir}/usr/lib" "${pkgname}/out-light/libhardened_malloc-light.so"

  install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" "${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
