# Maintainer: Albert I <kras@raphielgang.org>
# Contributor: Ali Molaei <ali dot molaei at protonmail dot com>

pkgname=python-proton-vpn-connection
pkgver=0.14.2
pkgrel=1
pkgdesc='Proton Technologies VPN connector for Linux'
arch=('any')
url='https://github.com/ProtonVPN/python-proton-vpn-connection'
license=('GPL-3.0-or-later')
groups=('ProtonVPN')
depends=(
    'python'
    'python-jinja'
    'python-proton-core'
    'python-proton-vpn-killswitch'
    'python-proton-vpn-logger'
)
makedepends=(
    'git'
    'python-build'
    'python-installer'
    'python-setuptools'
    'python-wheel'
)
_commit=6a365aeaf3e8f02df00aed1bd04a59512c2da174  # refs/tags/v0.14.2
source=("git+https://github.com/ProtonVPN/python-proton-vpn-connection.git#commit=${_commit}?signed")
sha256sums=('SKIP')
validpgpkeys=(
    '221CF155560AB2BA835FEAFADAC1874414C1A367'  # Pep Llaneras <josep.llaneras@proton.ch>
    '3DEF8AAF4E4379A665EB31F355F2D687A9C0C999'  # Alexandru Cheltuitor (Gitlab Protontech) <alexandru.cheltuitor@proton.ch>
)

build() {
    cd "${pkgname}"
    python -m build -w -n
}

package() {
    cd "${pkgname}"
    python -m installer -d "${pkgdir}" dist/*.whl
}
