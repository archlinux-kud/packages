# Maintainer: Albert I <kras@raphielgang.org>
# Contributor: Ali Molaei <ali dot molaei at protonmail dot com>

pkgname=python-proton-vpn-network-manager
pkgver=0.4.0
pkgrel=1
pkgdesc='Proton Technologies VPN NetworkManager plugin for Linux'
arch=('any')
url='https://github.com/ProtonVPN/python-proton-vpn-network-manager'
license=('GPL-3.0-or-later')
groups=('ProtonVPN')
depends=(
    'libnm'
    'python'
    'python-gobject'
    'python-proton-core'
    'python-proton-vpn-connection'
)
makedepends=(
    'python-build'
    'python-installer'
    'python-setuptools'
    'python-wheel'
)
optdepends=(
    'network-manager-applet: password prompt on other DEs'
    'plasma-nm: password prompt on KDE Plasma'
)
conflicts=('python-protonvpn-nm-lib')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha384sums=('73872bc60c4f8451370098f9a14140b5df3051614c448d4ffdb9d45f878fa48e066beb476577e80e426261afdb9cd01a')
b2sums=('30141a569c6e6ca3b7851c76dd850298a55f156abec182185d382d52fc96b2456f1f6ca641e6a79b619aed22d642bbfcb860865788e1246d944180ab74bd19cb')

build() {
    cd "${pkgname}-${pkgver}"
    python -m build -w -n
}

package() {
    cd "${pkgname}-${pkgver}"
    python -m installer -d "${pkgdir}" dist/*.whl
}
