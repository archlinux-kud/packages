# Maintainer: Albert I <kras@raphielgang.org>
# Contributor: Ali Molaei <ali dot molaei at protonmail dot com>

pkgname=python-proton-vpn-network-manager
pkgver=0.4.0
pkgrel=1
pkgdesc='Proton Technologies VPN NetworkManager plugin for Linux'
arch=('any')
url='https://github.com/ProtonVPN/python-proton-vpn-network-manager'
license=('GPL-3.0-or-later')
groups=('ProtonVPN')
depends=(
    'libnm'
    'python'
    'python-gobject'
    'python-proton-core'
    'python-proton-vpn-connection'
)
makedepends=(
    'git'
    'python-build'
    'python-installer'
    'python-setuptools'
    'python-wheel'
)
optdepends=(
    'network-manager-applet: password prompt on other DEs'
    'plasma-nm: password prompt on KDE Plasma'
)
conflicts=('python-protonvpn-nm-lib')
_commit=357b68a904e51dc1f4565b9a5e519445baa71a35  # refs/tags/v0.4.0
source=("git+https://github.com/ProtonVPN/python-proton-vpn-network-manager.git#commit=${_commit}?signed")
sha256sums=('SKIP')
validpgpkeys=('221CF155560AB2BA835FEAFADAC1874414C1A367')  # Pep Llaneras <josep.llaneras@proton.ch>

build() {
    cd "${pkgname}"
    python -m build -w -n
}

package() {
    cd "${pkgname}"
    python -m installer -d "${pkgdir}" dist/*.whl
}
