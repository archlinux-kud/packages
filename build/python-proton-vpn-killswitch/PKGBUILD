# Maintainer: Albert I <kras@raphielgang.org>
# Contributor: Ali Molaei <ali dot molaei at protonmail dot com>

pkgname=python-proton-vpn-killswitch
pkgver=0.4.0
pkgrel=1
pkgdesc='Proton Technologies VPN kill switch for Linux'
arch=('any')
url='https://github.com/ProtonVPN/python-proton-vpn-killswitch'
license=('GPL-3.0-or-later')
groups=('ProtonVPN')
depends=(
    'python'
    'python-proton-core'
)
makedepends=(
    'python-build'
    'python-installer'
    'python-setuptools'
    'python-wheel'
)
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha384sums=('b30b31ecb01046c926ee6ad7c54317e230138b46eb6aea1a121a947c882f8c5399acefdd70c4fbe62ff7ff94031d023d')
b2sums=('e1427bbff5e1e4b09be8ff6d6ce1708a9a9c7b8604470a06d2bf46656f54339b01694c2fee4ec836f027fe88b2192b61639b0adab687c9d759b352d5472b72b3')

build() {
    cd "${pkgname}-${pkgver}"
    python -m build -w -n
}

package() {
    cd "${pkgname}-${pkgver}"
    python -m installer -d "${pkgdir}" dist/*.whl
}
