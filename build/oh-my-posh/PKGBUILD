# Maintainer: Albert I <kras@raphielgang.org>
# Contributor: Jan0660 <jan0660@tutanota.com>

pkgname=oh-my-posh
pkgver=18.3.3
pkgrel=1
pkgdesc="A prompt theme engine for any shell"
arch=('x86_64')
url="https://ohmyposh.dev"
license=('MIT')
depends=('glibc')
makedepends=('go')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/JanDeDobbeleer/oh-my-posh/archive/refs/tags/v${pkgver}.tar.gz")
sha384sums=('ba0aa0d275598132541b4603778a55acee5237de3bd1a8761dc63cb2e62648e2f8367b01e8ac68e29508596b23159ec3')
b2sums=('7334a7b9a9c739f780bc96ed78d3ab03f3b714631216548db3de2f84e3af2c7d6fbfba7dd468fca3bcac66a9f29d5b4d0b8b21112d9f54bb57af15e057b6b486')

_gourl=github.com/jandedobbeleer/${pkgname}/src/build
_godate="$(date -u ${SOURCE_DATE_EPOCH:+-d @${SOURCE_DATE_EPOCH}} +"%Y-%m-%dT%H:%M:%SZ")"

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  cd "${srcdir}/${pkgname}-${pkgver}/src"

  # Only latest passed -ldflags= will be honored and -X variable doesn't work with GOFLAGS
  go build -ldflags="-X ${_gourl}.Version=${pkgver} -X ${_gourl}.Date=${_godate} -linkmode=external"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Install binary
  install -Dm755 src/src "${pkgdir}/usr/bin/${pkgname}"

  # Install default themes
  install -d "${pkgdir}/usr/share/${pkgname}"
  cp -r themes "${pkgdir}/usr/share/${pkgname}/themes"

  # Copy license
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
