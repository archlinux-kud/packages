# Maintainer: Albert I <kras@raphielgang.org>
# Contributor: Jan0660 <jan0660@tutanota.com>

pkgname=oh-my-posh
pkgver=18.2.0
pkgrel=1
pkgdesc="A prompt theme engine for any shell"
arch=('x86_64')
url="https://ohmyposh.dev"
license=('MIT')
depends=('glibc')
makedepends=('go')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/JanDeDobbeleer/oh-my-posh/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('3bc32ed93c7ce1623a20e152bbd6b0d2e98524d47dc44a2c23cfd6015c515cc9bffb3857f176fb1296d8ca2fcab50e85f8f75dca722aca6b8c5d606933598304')
b2sums=('71678a155c6820a923a2ee9a46578d9db3c24c2897a88910f846f3e55664fb99549d60b70e817eaaa8bbc5555b77b77be4e0da5db0156da04b8fe64d33211c8e')

_gourl=github.com/jandedobbeleer/${pkgname}/src/build
_godate="$(date -u ${SOURCE_DATE_EPOCH:+-d @${SOURCE_DATE_EPOCH}} +"%Y-%m-%dT%H:%M:%SZ")"

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  cd "${srcdir}/${pkgname}-${pkgver}/src"

  # Only latest passed -ldflags= will be honored and -X variable doesn't work with GOFLAGS
  go build -ldflags="-X ${_gourl}.Version=${pkgver} -X ${_gourl}.Date=${_godate} -linkmode=external"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Install binary
  install -Dm755 src/src "${pkgdir}/usr/bin/${pkgname}"

  # Install default themes
  install -d "${pkgdir}/usr/share/${pkgname}"
  cp -r themes "${pkgdir}/usr/share/${pkgname}/themes"

  # Copy license
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
