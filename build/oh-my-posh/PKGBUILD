# Maintainer: Albert I <kras@raphielgang.org>
# Contributor: Jan0660 <jan0660@tutanota.com>

pkgname=oh-my-posh
pkgver=18.3.5
pkgrel=1
pkgdesc="A prompt theme engine for any shell"
arch=('x86_64')
url="https://ohmyposh.dev"
license=('MIT')
depends=('glibc')
makedepends=('go')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/JanDeDobbeleer/oh-my-posh/archive/refs/tags/v${pkgver}.tar.gz")
sha384sums=('e44c48304353e15fa77bd957718136ead2bddde57f682b50a40448393003ecdf148fb5956b4482d25bb4240be1ce3242')
b2sums=('aadbfaa23d8102818740f7dfa038a412ab31b2aad6bdce6e9ab6a8a700fb5adb70a8acbb6ab1e16aa69914e45d2260512762c9a77905390bdf2f99c330f88377')

_gourl=github.com/jandedobbeleer/${pkgname}/src/build
_godate="$(date -u ${SOURCE_DATE_EPOCH:+-d @${SOURCE_DATE_EPOCH}} +"%Y-%m-%dT%H:%M:%SZ")"

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"

  cd "${srcdir}/${pkgname}-${pkgver}/src"

  go build -o "${pkgname}" \
    -buildmode=pie \
    -trimpath \
    -ldflags="-X ${_gourl}.Version=${pkgver} -X ${_gourl}.Date=${_godate} -linkmode=external" \
    -mod=readonly \
    -modcacherw
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Install binary
  install -Dm755 "src/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  # Install default themes
  install -d "${pkgdir}/usr/share/${pkgname}"
  cp -r themes "${pkgdir}/usr/share/${pkgname}/themes"

  # Copy license
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
