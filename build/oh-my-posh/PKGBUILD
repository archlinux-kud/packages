# Maintainer: Albert I <kras@raphielgang.org>
# Contributor: Jan0660 <jan0660@tutanota.com>

pkgname=oh-my-posh
pkgver=12.25.0
pkgrel=1
pkgdesc="A prompt theme engine for any shell."
arch=('x86_64')
url="https://ohmyposh.dev"
license=('custom:MIT')
depends=('glibc')
makedepends=('git' 'go')
source=("git+https://github.com/JanDeDobbeleer/oh-my-posh.git#tag=v$pkgver")
sha256sums=('SKIP')

prepare() {
  sed "s/development/$pkgver/" -i "$srcdir/oh-my-posh/src/main.go"
}

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  cd "$srcdir/oh-my-posh/src"

  go build
}

package() {
  cd "$srcdir/oh-my-posh"

  # install binary
  install -m755 -Dt "$pkgdir/usr/bin" src/oh-my-posh

  # install default themes
  install -d "$pkgdir/usr/share/oh-my-posh"
  cp -r themes "$pkgdir/usr/share/oh-my-posh/themes"

  # copy license
  install -m644 -Dt "$pkgdir/usr/share/licenses/$pkgname" COPYING
}
