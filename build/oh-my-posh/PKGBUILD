# Maintainer: Albert I <kras@raphielgang.org>
# Contributor: Jan0660 <jan0660@tutanota.com>

pkgname=oh-my-posh
pkgver=18.13.0
pkgrel=1
pkgdesc="A prompt theme engine for any shell"
arch=('x86_64')
url="https://ohmyposh.dev"
license=('MIT')
depends=('glibc')
makedepends=('go')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/JanDeDobbeleer/oh-my-posh/archive/refs/tags/v${pkgver}.tar.gz")
sha384sums=('fab8c239eb60c1bfdc5c76e733a0aa357985e699e4871a021fde3a482dc368410bfa6b99a1d81ae769e40227919be1d5')
b2sums=('7ebd90ae435b0b639ea71878cbbe1cfb1359c69c4c15ee39d891468ea4204ef039e85bf19d74949561ed573bafe4a74a87cbbcc6a90f4c115e237f1b3314112c')

_gourl=github.com/jandedobbeleer/${pkgname}/src/build
_godate="$(date -u ${SOURCE_DATE_EPOCH:+-d @${SOURCE_DATE_EPOCH}} +"%Y-%m-%dT%H:%M:%SZ")"

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"

  cd "${srcdir}/${pkgname}-${pkgver}/src"

  go build -o "${pkgname}" \
    -buildmode=pie \
    -trimpath \
    -ldflags="-X ${_gourl}.Version=${pkgver} -X ${_gourl}.Date=${_godate} -linkmode=external" \
    -mod=readonly \
    -modcacherw
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Install binary
  install -Dm755 "src/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  # Install default themes
  install -d "${pkgdir}/usr/share/${pkgname}"
  cp -r themes "${pkgdir}/usr/share/${pkgname}/themes"

  # Copy license
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
