# Maintainer: Albert I <kras@raphielgang.org>
# Contributor: Jan0660 <jan0660@tutanota.com>

pkgname=oh-my-posh
pkgver=18.23.3
pkgrel=1
pkgdesc="A prompt theme engine for any shell"
arch=('x86_64')
url="https://ohmyposh.dev"
license=('MIT')
depends=('glibc')
makedepends=('go')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/JanDeDobbeleer/oh-my-posh/archive/refs/tags/v${pkgver}.tar.gz")
sha384sums=('f01797b6ba7c8e9d75cf112b92452fe3b081cbb71dc05ad3a26bab4b83168c4a7ab37bccd5dcc5f782a6e29d7cf2fc7f')
b2sums=('d4183a10dadf34a4d9e5ae026a1f82875b2e3f590da8ced1387d7f8d65843b016829e2f9695e187b9afe8846de4a8634e84021cb995bf3903ce888759b25c417')

_gourl=github.com/jandedobbeleer/${pkgname}/src/build
_godate="$(date -u ${SOURCE_DATE_EPOCH:+-d @${SOURCE_DATE_EPOCH}} +"%Y-%m-%dT%H:%M:%SZ")"

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"

  cd "${srcdir}/${pkgname}-${pkgver}/src"

  go build -o "${pkgname}" \
    -buildmode=pie \
    -trimpath \
    -ldflags="-X ${_gourl}.Version=${pkgver} -X ${_gourl}.Date=${_godate} -linkmode=external" \
    -mod=readonly \
    -modcacherw
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Install binary
  install -Dm755 "src/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  # Install default themes
  install -d "${pkgdir}/usr/share/${pkgname}"
  cp -r themes "${pkgdir}/usr/share/${pkgname}/themes"

  # Copy license
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
