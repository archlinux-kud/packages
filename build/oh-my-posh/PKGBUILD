# Maintainer: Albert I <kras@raphielgang.org>
# Contributor: Jan0660 <jan0660@tutanota.com>

pkgname=oh-my-posh
pkgver=19.19.0
pkgrel=1
pkgdesc='A prompt theme engine for any shell'
arch=('x86_64')
url='https://ohmyposh.dev'
license=('MIT')
depends=(
  'glibc'
)
makedepends=(
  'git'
  'go'
)
source=("git+https://github.com/JanDeDobbeleer/oh-my-posh.git#tag=v${pkgver}")
sha384sums=('5dbcc6ab3caaad68e5a3ae145443883fb7ebb8f31780872239732c6d3aad417b3bbe6f1fa0683382726b70b25840c05d')
b2sums=('536a66ee956d333817e0c520e266122e4b361ddad1cdea893c5c43288f2fcbfe824258c71ee4f7cdf36d66306b30771995470d7cfcc9c15ead2e2071965644ad')

_gourl=github.com/jandedobbeleer/${pkgname}/src/build

prepare() {
  # Aids reproducible builds
  SOURCE_DATE_EPOCH=$(git -C oh-my-posh log -1 --pretty=%ct)

  _godate="$(date -ud @${SOURCE_DATE_EPOCH} +"%Y-%m-%dT%H:%M:%SZ")"
  export _godate
}

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"

  cd oh-my-posh/src

  go build -o "${pkgname}" \
    -buildmode=pie \
    -trimpath \
    -ldflags="-X ${_gourl}.Version=${pkgver} -X ${_gourl}.Date=${_godate} -linkmode=external" \
    -mod=readonly \
    -modcacherw
}

package() {
  cd oh-my-posh

  # Install binary
  install -Dt "${pkgdir}/usr/bin" "src/${pkgname}"

  # Install default themes
  install -Dt "${pkgdir}/usr/share/${pkgname}/themes" -m644 themes/*.omp.*

  # Copy license
  install -Dt "${pkgdir}/usr/share/licenses/${pkgname}" -m644 COPYING
}
