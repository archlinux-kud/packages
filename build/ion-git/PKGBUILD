# Maintainer: Albert I <kras@raphielgang.org>
# Contributor: Luca Weiss <luca (at) z3ntu (dot) xyz>

_pkgname=ion
pkgname=${_pkgname}-git
pkgver=r2185.gd7a9e795
pkgrel=1
pkgdesc='The Ion Shell. Compatible with Redox and Linux. (git version)'
arch=('i686' 'x86_64')
url='https://gitlab.redox-os.org/redox-os/ion'
license=('MIT')
depends=('gcc-libs' 'glibc')
makedepends=('cargo' 'git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
install=ion.install
source=('git+https://gitlab.redox-os.org/redox-os/ion.git')
sha256sums=('SKIP')

pkgver() {
    cd "${_pkgname}"
    printf 'r%s.g%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "${_pkgname}"
    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd "${_pkgname}"
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --release --frozen
}

check() {
    cd "${_pkgname}"
    export RUSTUP_TOOLCHAIN=stable

    cargo check --frozen --features=piston

    bash/version.sh
    RUSTUP=0 VENDORED=1 make tests
}

package() {
    install -Dm755 -t "${pkgdir}/usr/bin" "${_pkgname}/target/release/ion"
    install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" "${_pkgname}/LICENSE"
}
