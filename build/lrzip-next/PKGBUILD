# Maintainer: Albert I <kras@raphielgang.org>

# Based on original lrzip PKGBUILD by:
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: graysky <graysky@archlinux.us>
# Contributor: <kastor@fobos.org.ar>

pkgname=lrzip-next
pkgver=0.12.6
pkgrel=2
pkgdesc='Next generation of multi-threaded compression based on rzip'
url='https://github.com/pete4abw/lrzip-next'
license=('GPL-2.0-or-later')
arch=('x86_64')
depends=(
    'bash'
    'bzip2' 'libbz2.so'
    'bzip3' 'libbzip3.so'
    'gcc-libs'
    'glibc'
    'libgcrypt'
    'lz4'
    'lzo' 'liblzo2.so'
    'zlib' 'libz.so'
    'zstd' 'libzstd.so'
)
provides=('lrzip')
conflicts=('lrzip')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha384sums=('d60561b7870e726f5d6cb1bc59236774b00f1a1a7492aadd6464be2c1a42a2dd41802584c582d6f03b91feff73f93e15')
b2sums=('952b3aefcf424b4626f14caaefc7b160a271330903227207cec249306750dfd24097abfdae159d82dc86e08711f51789ca72bcc66b6d7f4bdd9b892133759dae')

build() {
  cd "${pkgname}-${pkgver}"

  CFLAGS="$CFLAGS -fomit-frame-pointer"
  CXXFLAGS="$CXXFLAGS -fomit-frame-pointer"

  ./autogen.sh --prefix=/usr "${flags}"
  make
}

package() {
  DESTDIR="${pkgdir}" make -C "${pkgname}-${pkgver}" install-strip
  ln -s lrzip-next "${pkgdir}/usr/bin/lrzip"
}

# vim: ts=2 sw=2 et:
# getver: -u 3 github.com/pete4abw/lrzip-next/blob/master/ChangeLog
