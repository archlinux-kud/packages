# Maintainer: Albert I <kras@raphielgang.org>

# Based on original lrzip PKGBUILD by:
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: graysky <graysky@archlinux.us>
# Contributor: <kastor@fobos.org.ar>

pkgname=lrzip-next
pkgver=0.12.3
pkgrel=1
pkgdesc='Next generation of multi-threaded compression based on rzip'
url='https://github.com/pete4abw/lrzip-next'
license=('GPL2')
arch=('x86_64')
depends=('bzip2' 'bzip3' 'lzo' 'zstd')
provides=('lrzip')
conflicts=('lrzip')
options=('lto')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha384sums=('0c56f42217e93f798d38fa9e5f43bc29d76e6d8593dc07f2964f2d42431191ca2fe9b6c57718b1a0021e55c5d41d3291')
b2sums=('df6de4f3d6e49738aa94822f84714434363ef59ed0ee744a07cd165d7dfdab50c2221ad37faa25f114839a92309da23438ca9da1f3f76d98182f55c825ad528c')

build() {
  cd "${pkgname}-${pkgver}"

  CFLAGS="$CFLAGS -fomit-frame-pointer"
  CXXFLAGS="$CXXFLAGS -fomit-frame-pointer"

  ./autogen.sh --prefix=/usr "${flags}"
  make
}

package() {
  DESTDIR="${pkgdir}" make -C "${pkgname}-${pkgver}" install-strip
  ln -s lrzip-next "${pkgdir}/usr/bin/lrzip"
}

# vim: ts=2 sw=2 et:
# getver: -u 3 github.com/pete4abw/lrzip-next/blob/master/ChangeLog
