# Maintainer: Albert I <kras@raphielgang.org>

# Based on original lrzip PKGBUILD by:
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: graysky <graysky@archlinux.us>
# Contributor: <kastor@fobos.org.ar>

pkgname=lrzip-next
pkgver=0.12.7
pkgrel=1
pkgdesc='Next generation of multi-threaded compression based on rzip'
url='https://github.com/pete4abw/lrzip-next'
license=('GPL-2.0-or-later')
arch=('x86_64')
depends=(
    'bash'
    'bzip2' 'libbz2.so'
    'bzip3' 'libbzip3.so'
    'gcc-libs'
    'glibc'
    'libgcrypt'
    'lz4'
    'lzo' 'liblzo2.so'
    'zlib' 'libz.so'
    'zstd' 'libzstd.so'
)
provides=('lrzip')
conflicts=('lrzip')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha384sums=('ba67635d2f0d467edd95dec6591f7d8bf5c4b244953ade729d09ddfe0e2121b2be1e4d4e870dc83345f7fca67ba2e3a3')
b2sums=('d15ad3bc959abeaf94089d357f61854a38b8e2b2a965e27cd37eba7a3986d27c502c64307177dd0ec622a49ab3abb7dde21d341e98fe0d851f6dae5c84daba56')

build() {
  cd "${pkgname}-${pkgver}"

  CFLAGS="$CFLAGS -fomit-frame-pointer"
  CXXFLAGS="$CXXFLAGS -fomit-frame-pointer"

  ./autogen.sh --prefix=/usr "${flags}"
  make
}

package() {
  DESTDIR="${pkgdir}" make -C "${pkgname}-${pkgver}" install-strip
  ln -s lrzip-next "${pkgdir}/usr/bin/lrzip"
}

# vim: ts=2 sw=2 et:
# getver: -u 3 github.com/pete4abw/lrzip-next/blob/master/ChangeLog
