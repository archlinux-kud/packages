# Maintainer: Albert I <kras@raphielgang.org>
# Contributor: Ali Molaei <ali dot molaei at protonmail dot com>

pkgname=python-proton-vpn-killswitch-network-manager
pkgver=0.4.3
pkgrel=1
pkgdesc='Proton Technologies VPN NetworkManager kill switch plugin for Linux'
arch=('any')
url='https://github.com/ProtonVPN/python-proton-vpn-killswitch-network-manager'
license=('GPL-3.0-or-later')
groups=('ProtonVPN')
depends=(
    'libnm'
    'python'
    'python-gobject'
    'python-packaging'
    'python-proton-vpn-connection'
    'python-proton-vpn-killswitch'
    'python-proton-vpn-logger'
)
makedepends=(
    'git'
    'python-build'
    'python-installer'
    'python-setuptools'
    'python-wheel'
)
_commit=9ae9a507ec182b6ff5f551714d108400d25a8aaa  # refs/tags/v0.4.3
source=("git+https://github.com/ProtonVPN/python-proton-vpn-killswitch-network-manager.git#commit=${_commit}?signed")
sha256sums=('SKIP')
validpgpkeys=('221CF155560AB2BA835FEAFADAC1874414C1A367')  # Pep Llaneras <josep.llaneras@proton.ch>

build() {
    cd "${pkgname}"
    python -m build -w -n
}

package() {
    cd "${pkgname}"
    python -m installer -d "${pkgdir}" dist/*.whl
}
