# Maintainer: Albert I <kras@raphielgang.org>

pkgname=python-sushi-sub
pkgver=0.6.2
pkgrel=3
pkgdesc='Automatic subtitle shifter based on audio'
url='https://github.com/FichteFoll/Sushi'
arch=('any')
license=('MIT')
depends=(
    'python'
    'python-numpy'
    'python-opencv'
)
makedepends=(
    'python-build'
    'python-installer'
    'python-setuptools'
    'python-wheel'
)
optdepends=(
    'ffmpeg: for any kind of demuxing'
    'mkvtoolnix-cli: for fast timecodes extraction when demuxing'
    'python-colorama: for colored outputs'
    'scxvid: for making keyframes'
)
conflicts=('sushi-shifter-git')
replaces=('sushi-shifter-git')
source=("Sushi-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz"
        "Sushi-np-ma-average.patch::https://patch-diff.githubusercontent.com/raw/FichteFoll/Sushi/pull/1.patch"
        "Sushi-ass-error-handling.patch::https://patch-diff.githubusercontent.com/raw/FichteFoll/Sushi/pull/3.patch")
sha384sums=('25183b7e5a9dcedc29bc6bcfd62e5acc4cfb262b1e2c9346549893093127a570db64c79f478ba658f6e0a501564b78ed'
            '29fdd4112d739ab75f595b00c32c8d6aa9df052e8d35125e910f2d79ebe969b87c188d9730d6386ca71c9dfa72d4aea2'
            'd7205a41dac74f256dec70af8cd720c1465b4ba770a9dd3d3721d98f05ae0ce7d38d31777e5abe4950edbe973d8acc9d')
b2sums=('66827c0acd9acbce591a1a6934e709897dac385063b2c32cf49ad67dc9f59c949a64c2bcddec9de48ca01bdca51eadcaca27cdaf2359ac307044c7825e74f489'
        'd65e0391c0404da4ca1817152acea212860e0651ab4db86c4451a05219014d69c4bf6833af8450c919d7f5b3d2edabd0ea70102a7d9347db648aebfad9ff7079'
        '8966a0264d41f225cfece7b8a651982aa2d5078f5c876466f1e20f65b12bcc7204f353d5206e17b2f512a28ec65aabeb1bf13401ff1d5dbf5d851af0a9ebbe4f')

_python_version() {
    python3 <(\
        cat << 'EOF'
import sys
vi = sys.version_info
print(f"{vi.major}.{vi.minor}")
EOF
    )
}

prepare() {
    # Avoid an edge case of division-by-zero error
    patch -d "${srcdir}/Sushi-${pkgver}" -N -p1 <"${srcdir}/Sushi-np-ma-average.patch"

    # Fix ASS error handling (remnants of 2to3 conversion)
    patch -d "${srcdir}/Sushi-${pkgver}" -N -p1 <"${srcdir}/Sushi-ass-error-handling.patch"
}

build() {
    cd "${srcdir}/Sushi-${pkgver}"
    python -m build -w -n
}

check() {
    cd "${srcdir}/Sushi-${pkgver}"
    # python -m unittest discover doesn't find anything
    python -m unittest tests.{timecodes,main,subtitles,demuxing}
}

package() {
    local _py=$(_python_version)

    cd "${srcdir}/Sushi-${pkgver}"
    python -m installer -d "${pkgdir}" dist/*.whl

    # Rename wrapper to avoid conflicts with sushi from [extra]
    mv "${pkgdir}"/usr/bin/sushi{,-sub}

    # Symlink LICENSE from wheel dist-info copy
    install -d "${pkgdir}/usr/share/licenses/${pkgname}"
    ln -s "../../../lib/python${_py}/site-packages/sushi_sub-${pkgver}.dist-info/LICENSE" \
        "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
