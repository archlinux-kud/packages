# Maintainer: Albert I <kras@raphielgang.org>
# Contributor: Ali Molaei <ali dot molaei at protonmail dot com>

pkgname=python-proton-vpn-api-core
pkgver=0.21.0
pkgrel=1
pkgdesc='Proton Technologies VPN core API'
arch=('any')
url='https://github.com/ProtonVPN/python-proton-vpn-api-core'
license=('GPL-3.0-or-later')
groups=('ProtonVPN')
depends=(
    'python'
    'python-distro'
    'python-proton-core'
    'python-proton-vpn-connection'
    'python-proton-vpn-killswitch'
    'python-proton-vpn-logger'
    'python-proton-vpn-session'
)
makedepends=(
    'git'
    'python-build'
    'python-installer'
    'python-setuptools'
    'python-wheel'
)
_commit=88594aeb13cce274a146e807a88ad38ac6e78b2c  # refs/tags/v0.21.0
source=("git+https://github.com/ProtonVPN/python-proton-vpn-api-core.git#commit=${_commit}?signed")
sha256sums=('SKIP')
validpgpkeys=(
    '221CF155560AB2BA835FEAFADAC1874414C1A367'  # Pep Llaneras <josep.llaneras@proton.ch>
    '3DEF8AAF4E4379A665EB31F355F2D687A9C0C999'  # Alexandru Cheltuitor (Gitlab Protontech) <alexandru.cheltuitor@proton.ch>
)

build() {
    cd "${pkgname}"
    python -m build -w -n
}

package() {
    cd "${pkgname}"
    python -m installer -d "${pkgdir}" dist/*.whl
}
