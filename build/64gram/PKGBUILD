# PKGBUILD is based on telegram-desktop by:
# - Sven-Hendrik Haase <svenstaro@archlinux.org>
# - hexchain <i@hexchain.org>

# Maintainer: Albert I <kras@raphielgang.org>

_tdesktop_ver=4.5.3
pkgname=64gram
pkgver=1.0.63
_pkgver=${pkgver/.beta}
pkgrel=1
pkgdesc='Provide Windows 64bit build with enhancements. Linux/MacOS build is a PLUS!'
arch=('x86_64')
url="https://64gr.am"
license=('GPL3')
depends=('hunspell' 'ffmpeg' 'hicolor-icon-theme' 'lz4' 'minizip' 'openal' 'ttf-opensans'
         'qt6-imageformats' 'qt6-svg' 'qt6-wayland' 'qt6-5compat' 'xxhash' 'glibmm-2.68'
         'rnnoise' 'pipewire' 'libxtst' 'libxrandr' 'jemalloc' 'abseil-cpp' 'libdispatch'
         'openssl-1.1' 'protobuf')
makedepends=('cmake' 'git' 'ninja' 'python' 'range-v3' 'tl-expected' 'microsoft-gsl' 'meson'
             'extra-cmake-modules' 'wayland-protocols' 'plasma-wayland-protocols' 'libtg_owt')
optdepends=('webkit2gtk: embedded browser features'
            'xdg-desktop-portal: desktop integration')
provides=("telegram-desktop=$_tdesktop_ver")
replaces=('tdesktop-x64' '64gram-desktop')
conflicts=('telegram-desktop' 'forkgram')
source=("https://github.com/TDesktop-x64/tdesktop/releases/download/v${_pkgver}/64Gram-${_pkgver}-full.tar.gz")
b2sums=('b412d38ef1871fe8fa53c13efc3b85af8efc78c18dbf7e3e2bf359dcc2434a0dcd8f6a1aab942a17cc36f3b7db0b64d5a59a99491d8c371583d3ecdffc6d552c')

prepare() {
    cd "64Gram-${_pkgver}-full"
    rm -rf Telegram/ThirdParty/libtgvoip/webrtc_dsp/absl
}

build() {
    cd "64Gram-${_pkgver}-full"

    export PKG_CONFIG_PATH='/usr/lib/openssl-1.1/pkgconfig'
    # Turns out we're allowed to use the official API key that telegram uses for their snap builds:
    # https://github.com/telegramdesktop/tdesktop/blob/8fab9167beb2407c1153930ed03a4badd0c2b59f/snap/snapcraft.yaml#L87-L88
    # Thanks @primeos!
    cmake \
        -B build \
        -G Ninja \
        -DCMAKE_INSTALL_PREFIX="/usr" \
        -DCMAKE_BUILD_TYPE=Release \
        -DTDESKTOP_API_ID=611335 \
        -DTDESKTOP_API_HASH=d524b414d21f4d37f08684c1df41ac9c
    ninja -C build
}

package() {
    cd "64Gram-${_pkgver}-full"
    DESTDIR="$pkgdir" ninja -C build install
    # They botched the release and put a lot of stuff here.
    rm -rf "$pkgdir/build"
}
