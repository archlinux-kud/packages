# PKGBUILD is based on telegram-desktop by:
# - Sven-Hendrik Haase <svenstaro@archlinux.org>
# - hexchain <i@hexchain.org>

# Maintainer: Albert I <kras@raphielgang.org>

# Only bump if changelog explicitly says a TDesktop version
_tdesktop_ver=4.8.10
# We need to fetch our own cppgir copy here *shrugs*
_cppgir_commit=ada922a260479da27c989e39ed47e17a36cc2969
# TDesktop currently depends on unstable glibmm
_glibmm_ver=2.77.0

pkgname=64gram
pkgver=1.0.88
_pkgver=${pkgver/.beta}
pkgrel=1
pkgdesc='Unofficial 64-bit Telegram Desktop build with some enhancements'
arch=('x86_64')
url="https://64gr.am"
license=('GPL3')
depends=('hunspell' 'ffmpeg' 'hicolor-icon-theme' 'lz4' 'minizip' 'openal' 'ttf-opensans'
         'qt6-imageformats' 'qt6-svg' 'qt6-wayland' 'xxhash' 'rnnoise'
         'pipewire' 'libxtst' 'libxrandr' 'jemalloc' 'abseil-cpp' 'libdispatch' 'openssl'
         'protobuf' 'glib2' 'libsigc++-3.0' 'xz')
makedepends=('cmake' 'git' 'ninja' 'python' 'range-v3' 'tl-expected' 'microsoft-gsl' 'meson'
             'extra-cmake-modules' 'wayland-protocols' 'plasma-wayland-protocols' 'libtg_owt'
             'gobject-introspection' 'boost' 'fmt' 'mm-common' 'perl-xml-parser')
optdepends=('webkit2gtk: embedded browser features'
            'xdg-desktop-portal: desktop integration')
provides=("telegram-desktop=${_tdesktop_ver}")
replaces=('tdesktop-x64' '64gram-desktop')
conflicts=('telegram-desktop' 'forkgram')
source=("https://github.com/TDesktop-x64/tdesktop/releases/download/v${_pkgver}/64Gram-${_pkgver}-full.tar.gz"
        "expected-lite-${_tdesktop_ver}.tar.gz::https://github.com/martinmoene/expected-lite/archive/refs/tags/v0.6.3.tar.gz"
        "https://gitlab.com/mnauw/cppgir/-/archive/${_cppgir_commit}/cppgir-${_cppgir_commit}.tar.bz2"
        "https://download.gnome.org/sources/glibmm/${_glibmm_ver%.*}/glibmm-${_glibmm_ver}.tar.xz")
noextract=("expected-lite-${_tdesktop_ver}.tar.gz"
           "cppgir-${_cppgir_commit}.tar.bz2")
sha512sums=('f1e470e6e6c9d921c10b827251bec827afb22fb0bf57ed8ee3b34b2b9237e2e6a3737d7329349da5f15a427edf241899df32ec80cd81c52e4a9bc8cd15a73fee'
            'd6a4f30f90494dda002ad27d227f17ce0201752178418d7dfada26441e853590d46816c88922e7d458dda68ad4414ddfe6b7fa4ed2a5854e4e3b22675b13f92a'
            '90378f73a7c05e8a4fdadddf0add35ffb90fce563eab97ef981d6f9dfbbc4a5d0e83c70a54de9fbbf23d85eb6ce02036e0ffbfafe67384fbc99a997fd8b92f0b'
            '6650e822de2529582d93291025500afb6a182a0c5a564f656f164d79d8765bb4ca9c9d16227148431cc71c2677923b9364e81bbd4ca4f07f68e36bb380fb9574')
b2sums=('76a4a3263443c8c2a451685eb80b385b9c6c23625152b9811e2d727d4fe94c806dbf63b15b7fafcbc0c3da47a09288c9ad98bd23f0a2e0446ab01a3c240ca35e'
        '7b9eb948d674d4fb4904c6deb4f2c8c2d578d73d8401dc386a49c5f09cc75c6fd70e0c6aa72cd1e1fb9d82cf55917c8a6d60868190ba063340eda4f3191ebc56'
        '73fe45188c8304da84cc39b838176a9100622f3a2aa6cdb7a8eb4c58bb16fe5a8772c8bdc0d7686a322d86bc1aba3b22c3b1ebfeab98e7d282f7258050249a50'
        'c9c48248e78b90101bb546474837f8f60bba4b8cd6b2b019e919964a9f0a2fb967cb9d2b61ca449587db01ddbeca8eb31147467cfe744ea071ba43a09badda72')

prepare() {
    # Replace bundled cppgir with external revision that works for us
    rm -rf "64Gram-${_pkgver}-full/cmake/external/glib/cppgir"
    mkdir -p "64Gram-${_pkgver}-full/cmake/external/glib/cppgir/expected-lite"
    tar xf "expected-lite-${_tdesktop_ver}.tar.gz" -C "64Gram-${_pkgver}-full/cmake/external/glib/cppgir/expected-lite" --strip-components 1
    tar xf "cppgir-${_cppgir_commit}.tar.bz2" -C "64Gram-${_pkgver}-full/cmake/external/glib/cppgir" --strip-components 1
}

build() {
    CXXFLAGS+=' -ffat-lto-objects'

    # Telegram currently needs unstable glibmm so we bundle it in as static libs.
    # This isn't great but what can you do.
    meson setup -D maintainer-mode=true --default-library static --prefix "${srcdir}/glibmm" glibmm-2.77.0 glibmm-build
    meson compile -C glibmm-build
    meson install -C glibmm-build

    # Turns out we're allowed to use the official API key that telegram uses for their snap builds:
    # https://github.com/telegramdesktop/tdesktop/blob/8fab9167beb2407c1153930ed03a4badd0c2b59f/snap/snapcraft.yaml#L87-L88
    # Thanks @primeos!
    PKG_CONFIG_PATH="${srcdir}/glibmm/usr/local/lib/pkgconfig" \
    cmake -B build -S "64Gram-${_pkgver}-full" -G Ninja \
        -DCMAKE_VERBOSE_MAKEFILE=ON \
        -DCMAKE_INSTALL_PREFIX="/usr" \
        -DCMAKE_PREFIX_PATH="${srcdir}/glibmm" \
        -DCMAKE_BUILD_TYPE=Release \
        -DTDESKTOP_API_ID=611335 \
        -DTDESKTOP_API_HASH=d524b414d21f4d37f08684c1df41ac9c

    cmake --build build
}

package() {
    DESTDIR="${pkgdir}" cmake --install build
}
