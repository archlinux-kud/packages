# PKGBUILD is based on telegram-desktop by:
# - Sven-Hendrik Haase <svenstaro@archlinux.org>
# - hexchain <i@hexchain.org>

# Maintainer: Albert I <kras@raphielgang.org>

# Only bump if changelog explicitly says a TDesktop version
_tdesktop_ver=4.8.3
# Keep this on track with telegram-desktop PKGBUILD
_cppgir_commit=960fe054ffaab7cf55722fea6094c56a8ee8f18e

pkgname=64gram
pkgver=1.0.85
_pkgver=${pkgver/.beta}
pkgrel=2
pkgdesc='Provide Windows 64bit build with some enhancements.'
arch=('x86_64')
url="https://64gr.am"
license=('GPL3')
depends=('hunspell' 'ffmpeg' 'hicolor-icon-theme' 'lz4' 'minizip' 'openal' 'ttf-opensans'
         'qt6-imageformats' 'qt6-svg' 'qt6-wayland' 'xxhash' 'glibmm-2.68' 'rnnoise'
         'pipewire' 'libxtst' 'libxrandr' 'jemalloc' 'abseil-cpp' 'libdispatch' 'openssl'
         'protobuf')
makedepends=('cmake' 'git' 'ninja' 'python' 'range-v3' 'tl-expected' 'microsoft-gsl' 'meson'
             'extra-cmake-modules' 'wayland-protocols' 'plasma-wayland-protocols' 'libtg_owt'
             'gobject-introspection' 'glib2' 'boost' 'fmt')
optdepends=('webkit2gtk: embedded browser features'
            'xdg-desktop-portal: desktop integration')
provides=("telegram-desktop=$_tdesktop_ver")
replaces=('tdesktop-x64' '64gram-desktop')
conflicts=('telegram-desktop' 'forkgram')
source=("https://github.com/TDesktop-x64/tdesktop/releases/download/v${_pkgver}/64Gram-${_pkgver}-full.tar.gz"
        "expected-lite-${_tdesktop_ver}.tar.gz::https://github.com/martinmoene/expected-lite/archive/refs/tags/v0.6.3.tar.gz"
        "https://gitlab.com/mnauw/cppgir/-/archive/${_cppgir_commit}/cppgir-${_cppgir_commit}.tar.gz")
noextract=("expected-lite-${_tdesktop_ver}.tar.gz"
           "cppgir-${_cppgir_commit}.tar.gz")
sha512sums=('f4b2e281879994e43836bb2bab320de8166495e696a5230a8ec67b549fcf27a43f8800563e3449803ee363052a1c6a6c6ec8495f86d53892f6f5063ebccc1bf1'
            'd6a4f30f90494dda002ad27d227f17ce0201752178418d7dfada26441e853590d46816c88922e7d458dda68ad4414ddfe6b7fa4ed2a5854e4e3b22675b13f92a'
            '8ed54513511ec8ce6d7c9c311924b6d662102ffec3af75ccc8c4ebc6d48aaf162fd004b9c5fd31e50b83bc5872e82674eeaa2766423d9dc7d2338eb941ae8d40')
b2sums=('4ac42042dd15c4e565254634f3d284daa1c689a244a15c95f53e29fac810c506d97837799bb574458a395bccc377de30525b8f31ef509b429f7990008041b8c7'
        '7b9eb948d674d4fb4904c6deb4f2c8c2d578d73d8401dc386a49c5f09cc75c6fd70e0c6aa72cd1e1fb9d82cf55917c8a6d60868190ba063340eda4f3191ebc56'
        'db422e9921473ca2a33fda0fc9334d11786173c165807b0e7291b5f5fbe38032fbacee64421282a4ef50d7de15c805221e77cce022c629bbfd64d947c0aa2a36')

prepare() {
    # Remove bundled cppgir that doesn't generate correct headers
    rm -rf "64Gram-${_pkgver}-full/cmake/external/glib/cppgir"

    # Replace with external cppgir revision and related dependency that works on Arch Linux
    # Revision/release tarballs are correct as of telegram-desktop @ 251e99fd425396309db61971e7635d78613cb959
    mkdir -p "64Gram-${_pkgver}-full/cmake/external/glib/cppgir/expected-lite"
    tar xf "expected-lite-${_tdesktop_ver}.tar.gz" -C "64Gram-${_pkgver}-full/cmake/external/glib/cppgir/expected-lite" --strip-components 1
    tar xf "cppgir-${_cppgir_commit}.tar.gz" -C "64Gram-${_pkgver}-full/cmake/external/glib/cppgir" --strip-components 1
}

build() {
    CXXFLAGS+=' -ffat-lto-objects'

    # Turns out we're allowed to use the official API key that telegram uses for their snap builds:
    # https://github.com/telegramdesktop/tdesktop/blob/8fab9167beb2407c1153930ed03a4badd0c2b59f/snap/snapcraft.yaml#L87-L88
    # Thanks @primeos!
    cmake -B build -S "64Gram-${_pkgver}-full" -G Ninja \
        -DCMAKE_VERBOSE_MAKEFILE=ON \
        -DCMAKE_INSTALL_PREFIX="/usr" \
        -DCMAKE_BUILD_TYPE=Release \
        -DTDESKTOP_API_ID=611335 \
        -DTDESKTOP_API_HASH=d524b414d21f4d37f08684c1df41ac9c

    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
}
