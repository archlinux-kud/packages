# PKGBUILD is based on telegram-desktop by:
# - Sven-Hendrik Haase <svenstaro@archlinux.org>
# - hexchain <i@hexchain.org>

# Maintainer: Albert I <kras@raphielgang.org>

# Only bump if changelog explicitly says a TDesktop version
_tdesktop_ver=4.12.2
# We need to fetch our own cppgir copy here *shrugs*
_cppgir_commit=69ef481cba38e848554cc5403b8d4141d3c51335
_expected_lite_ver=0.6.3

pkgname=64gram-desktop
pkgver=1.1.5
_pkgver=${pkgver/.beta}
pkgrel=1
pkgdesc='Unofficial 64-bit Telegram Desktop build with some enhancements'
arch=('x86_64')
url="https://64gr.am"
license=('GPL3')
depends=('abseil-cpp' 'ffmpeg' 'glib2' 'glibmm-2.68' 'hicolor-icon-theme' 'hunspell' 'jemalloc'
         'libdispatch' 'libsigc++-3.0' 'libxcomposite' 'libxrandr' 'libxtst' 'lz4' 'minizip'
         'openal' 'openssl' 'pipewire' 'protobuf' 'qt6-imageformats' 'qt6-svg' 'qt6-wayland'
         'rnnoise' 'ttf-opensans' 'xxhash' 'xz')
makedepends=('boost' 'cmake' 'extra-cmake-modules' 'fmt' 'git' 'gobject-introspection' 'libtg_owt'
             'meson' 'microsoft-gsl' 'mm-common' 'ninja' 'perl-xml-parser'
             'plasma-wayland-protocols' 'python' 'range-v3' 'tl-expected' 'wayland-protocols')
optdepends=('webkit2gtk: embedded browser features'
            'xdg-desktop-portal: desktop integration')
provides=("telegram-desktop=${_tdesktop_ver}")
replaces=('tdesktop-x64' '64gram<1.0.93')
conflicts=('telegram-desktop')
source=("https://github.com/TDesktop-x64/tdesktop/releases/download/v${_pkgver}/64Gram-${_pkgver}-full.tar.gz"
        "expected-lite-${_expected_lite_ver}.tar.gz::https://github.com/martinmoene/expected-lite/archive/refs/tags/v${expected_lite_ver}.tar.gz"
        "https://gitlab.com/mnauw/cppgir/-/archive/${_cppgir_commit}/cppgir-${_cppgir_commit}.tar.bz2"
        "${pkgname}-fix-lzma-link.patch::https://aur.archlinux.org/cgit/aur.git/plain/fix-lzma-link.patch?h=64gram-desktop&id=480c0d51d97e2810610869e6c1349c1b3f26fdb5")
noextract=("expected-lite-${_expected_lite_ver}.tar.gz"
           "cppgir-${_cppgir_commit}.tar.bz2")
sha384sums=('e23d66ab1f97c3a776a00f8a9b06d23d82b5b8c44265c22599e1818a2ca7f6896456ffad23309cbab248f106af3be37d'
            '6454e7b41a8319d7bef88352539ae11d6e050e4bef9379890fa53697fe83996b18d6f9681b0e079c64253f85a571d619'
            '04b272d85c9a9796c617053dc752f48f483f2441979e4d02e8f4d48a3f1b63e68f31000e9bbd972397137555a29de89d'
            'fc99bc01e59380551939787c62afdced8cf3818a82d080f61c3d91fdee7146251bb52ece03acf04d25098118272e5a8e')
b2sums=('67937bce7348870fa184a8ca51f7098134d0b6aaeef888520214ef9c4b5f6eba385dfa5d1ff5a34c36d2fbee6f806af0c967b3d0dd494fe53678a36fb3f6a258'
        '7b9eb948d674d4fb4904c6deb4f2c8c2d578d73d8401dc386a49c5f09cc75c6fd70e0c6aa72cd1e1fb9d82cf55917c8a6d60868190ba063340eda4f3191ebc56'
        'b34386e6beb76c68f4f52395fcca80583e56ef3fd4a327724bc19253e64fab3419b064d4e7371213e9e80c6ab233ad3d6290e07271c1dcdf90b5ae00d23bd481'
        '184f9e72e2092b4f7e28490adf94472729b75973daa607c1033d79ddc3416d16670acedfb5e6ea66d9299211fe1b10b7deba34e9edc79198fbccfc98d2555730')

prepare() {
    # Replace bundled cppgir with external revision that works for us
    rm -rf "64Gram-${_pkgver}-full/cmake/external/glib/cppgir"
    mkdir -p "64Gram-${_pkgver}-full/cmake/external/glib/cppgir/expected-lite"
    tar xf "expected-lite-${_expected_lite_ver}.tar.gz" -C "64Gram-${_pkgver}-full/cmake/external/glib/cppgir/expected-lite" --strip-components 1
    tar xf "cppgir-${_cppgir_commit}.tar.bz2" -C "64Gram-${_pkgver}-full/cmake/external/glib/cppgir" --strip-components 1

    # Fix linking with LZMA if auto-update is disabled
    patch -d "64Gram-${_pkgver}-full" -N -p1 --binary <"${pkgname}-fix-lzma-link.patch"
}

build() {
    CXXFLAGS+=' -ffat-lto-objects'

    # Turns out we're allowed to use the official API key that telegram uses for their snap builds:
    # https://github.com/telegramdesktop/tdesktop/blob/8fab9167beb2407c1153930ed03a4badd0c2b59f/snap/snapcraft.yaml#L87-L88
    # Thanks @primeos!
    cmake -B build -S "64Gram-${_pkgver}-full" -G Ninja \
        -DCMAKE_VERBOSE_MAKEFILE=ON \
        -DCMAKE_INSTALL_PREFIX="/usr" \
        -DCMAKE_BUILD_TYPE=Release \
        -DDESKTOP_APP_DISABLE_AUTOUPDATE=ON \
        -DTDESKTOP_API_ID=611335 \
        -DTDESKTOP_API_HASH=d524b414d21f4d37f08684c1df41ac9c

    cmake --build build
}

package() {
    DESTDIR="${pkgdir}" cmake --install build
    install -Dm644 /dev/null "$pkgdir/etc/tdesktop/externalupdater"
}
